apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compile_sdk_version
    buildToolsVersion rootProject.build_tool_version

    defaultConfig {
        applicationId "ru.assisttech.assistsdk.github"
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.target_sdk_version
        versionCode rootProject.version_code
        versionName rootProject.version_name
    }
    //
    // Signing Note (Test)
    //
    // 1. Put file gradle.properties into /home/user/.gradle directory
    // 2. Fill in gradle.properties with string:
    //        TestAssistSDK.properties=/home/user/.signing/file_name.properties
    //        (Windows: TestAssistSDK.properties=C:\\Users\\user\\.signing\\file_name.properties)
    //
    // 3. Create directory /home/user/.signing
    // 4. Put into it file called file_name.properties
    // 5. Fill in file_name.properties with string-value pairs:
    //        keystore=/path/to/keystore/file.keystore (Windows: keystore=C:\\path\\to\\keystore\\file.keystore)
    //        keystore.password=your_password
    //        keyalias=your_key_alias
    //        keypassword=your_key_password
    //
    if (project.hasProperty("TestAssistSDK.properties") &&
            new File((String) project.property("TestAssistSDK.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("TestAssistSDK.properties"))))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyalias']
                keyPassword props['keypassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (System.getenv("TRAVIS")) {
                signingConfig null
            } else {
                signingConfig signingConfigs.release
            }
            zipAlignEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "mobilePayTest-${variant.name}-${variant.versionName}.apk"
        }
    }
    productFlavors {
    }

}

dependencies {
    implementation project(':sdk')
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'io.card:android-sdk:5.5.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    // Google Wallet
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-wallet:18.0.0'

    implementation 'com.github.mklimek:ssl-utils-android:1.0.1'

    implementation 'com.google.code.gson:gson:2.8.6'

    // Optional backport
    implementation 'com.github.dmstocking:support-optional:1.2'
}
